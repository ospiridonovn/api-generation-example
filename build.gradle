plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "org.openapi.generator" version "4.3.1"
}

group = 'com.github.ospiridonovn'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/src/main/resources/openapi.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = 'com.github.ospiridonovn.apigenerationexample.api'
    modelPackage = 'com.github.ospiridonovn.apigenerationexample.model'

    configOptions = [
            dateLibrary: "java8",
            interfaceOnly: "true",
            useBeanValidation: "true",
            skipDefaultInterface: "true",
            serializationLibrary: "jackson"
    ]
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.0'
    compile 'io.swagger:swagger-annotations:1.5.22'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

compileJava.dependsOn(tasks.openApiGenerate)

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}